plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.2.6"
    id "checkstyle"
    id "findbugs"
    id "jacoco"
    id "com.github.kt3k.coveralls" version "2.4.0"
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/embulk-input-jdbc/maven" }  // for embulk-input-jdbc
}

// TODO: Configure its Maven package group.
// group = ""
version = "0.3.0"
description = "Translate schema and column values by SQL-like query provided by Apache Calcite."

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    compile("org.embulk.input.jdbc:embulk-input-jdbc:0.8.2") {
        exclude group: "org.embulk", module: "embulk-core"
    }
    compile("org.apache.calcite:calcite-core:1.13.0") {
        // They conflict with embulk-core.
        exclude group: "org.slf4j", module:"slf4j-api"
        exclude group: "org.apache.commons", module: "commons-lang3"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.google.guava", module: "guava"
    }

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-core:0.9.23:tests"
    testCompile "org.embulk:embulk-standards:0.9.23"
    testCompile "org.embulk:embulk-test:0.9.23"

    // TODO: Remove them.
    // They are now required because the dependency libraries of them are behind sub ClassLoaders.
    testCompile "org.embulk:embulk-deps-buffer:0.9.23"
    testCompile "org.embulk:embulk-deps-config:0.9.23"
}

embulkPlugin {
    mainClass = "org.embulk.filter.calcite.CalciteFilterPlugin"
    category = "filter"
    type = "calcite"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
    repositories {
        // TODO: Configure some public Maven repo(s).
    }
}

gem {
    authors = [ "Muga Nishizawa" ]
    email = [ "muga.nishizawa@gmail.com" ]
    summary = "Apache Calcite filter plugin for Embulk"
    homepage = "https://github.com/muga/embulk-filter-calcite"
    licenses = [ "Apache 2.0" ]
}

gemPush {
    host = "https://rubygems.org"
}

def checkstyleConfigDir = new File(rootDir, "/config/checkstyle")
checkstyle {
    configFile = new File(checkstyleConfigDir, "checkstyle.xml")
    configProperties.checkstyleConfigDir = checkstyleConfigDir
    toolVersion = '7.6.1'
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
check.dependsOn('checkstyle')

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
findbugs {
    ignoreFailures = true
}

jacocoTestReport {
    reports {
        xml.enabled = true // coveralls plugin depends on xml format report
        html.enabled = true
    }
}
