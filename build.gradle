plugins {
    id "java"
    id "maven-publish"
    id "signing"
    id "org.embulk.embulk-plugins" version "0.4.2"
    id "checkstyle"
    id "jacoco"
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.5.0-SNAPSHOT"
description = "Translate schema and column values by SQL-like query provided by Apache Calcite."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.31"
    compileOnly "org.embulk:embulk-spi:0.10.31"

    compile("org.embulk:embulk-input-jdbc:0.12.1") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.31.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    compile("org.embulk:embulk-util-config:0.3.0") {
        // They conflict with embulk-core. They are once excluded here,
        // and added explicitly with versions exactly the same with embulk-core:0.10.31.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.datatype", module: "jackson-datatype-jdk8"
        exclude group: "javax.validation", module: "validation-api"
    }

    compile("org.apache.calcite:calcite-core:1.13.0") {
        // slf4j-api conflicts with embulk-api.
        exclude group: "org.slf4j", module:"slf4j-api"

        // Apache Commons Lang 3 conflicts with embulk-core till 0.10.31 (in plan).
        // They are once excluded here, and added explicitly with versions exactly the same with embulk-core:0.10.31.
        exclude group: "org.apache.commons", module: "commons-lang3"

        // Those Jackson libraries conflict with embulk-core till 0.10.31 (in plan).
        // They are once excluded here, and added explicitly with versions exactly the same with embulk-core:0.10.31.
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"

        // Guava conflicts with embulk-core till 0.10.33 (in plan).
        // They are once excluded here, and added explicitly with versions exactly the same with embulk-core:0.10.31.
        exclude group: "com.google.guava", module: "guava"
    }

    // They are once excluded from transitive dependencies of other dependencies,
    // and added explicitly with versions exactly the same with embulk-core:0.10.31.
    compile "org.apache.commons:commons-lang3:3.4"
    compile "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-core:2.6.7"
    compile "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    compile "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    compile "javax.validation:validation-api:1.1.0.Final"
    compile "com.google.guava:guava:18.0"

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-core:0.10.31"
    testCompile "org.embulk:embulk-core:0.10.31:tests"
    testCompile "org.embulk:embulk-junit4:0.10.31"
    testCompile "org.embulk:embulk-deps:0.10.31"

    testCompile "org.embulk:embulk-formatter-csv:0.10.31"
    testCompile "org.embulk:embulk-input-file:0.10.31"
    testCompile "org.embulk:embulk-output-file:0.10.31"
    testCompile "org.embulk:embulk-parser-csv:0.10.31"
}

embulkPlugin {
    mainClass = "org.embulk.filter.calcite.CalciteFilterPlugin"
    category = "filter"
    type = "calcite"
}

jar {
    from rootProject.file("LICENSE")
}

sourcesJar {
    from rootProject.file("LICENSE")
}

javadocJar {
    from rootProject.file("LICENSE")
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "Muga Nishizawa"
                        email = "muga.nishizawa@gmail.com"
                    }
                    developer {
                        name = "Dai MIKURUBE"
                        email = "dmikurube@treasure-data.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-filter-calcite.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-filter-calcite.git"
                    url = "https://github.com/embulk/embulk-filter-calcite"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

gem {
    authors = [ "Muga Nishizawa", "Dai MIKURUBE" ]
    email = [ "muga.nishizawa@gmail.com", "dmikurube@treasure-data.com" ]
    summary = "Apache Calcite filter plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-filter-calcite"
    licenses = [ "Apache-2.0" ]
}

gemPush {
    host = "https://rubygems.org"
}

def checkstyleConfigDir = new File(rootDir, "/config/checkstyle")
checkstyle {
    configFile = new File(checkstyleConfigDir, "checkstyle.xml")
    configProperties.checkstyleConfigDir = checkstyleConfigDir
    toolVersion = '7.6.1'
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
check.dependsOn('checkstyle')

jacocoTestReport {
    reports {
        html.enabled = true
    }
}
