plugins {
    id "com.jfrog.bintray" version "1.7"
    id "com.github.jruby-gradle.base" version "0.1.5"
    id "java"
    id "checkstyle"
    id "findbugs"
    id "jacoco"
}
import com.github.jrubygradle.JRubyExec

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/embulk-input-jdbc/maven" }  // for embulk-input-jdbc
}
configurations {
    provided
}

version = "0.1.3"

sourceCompatibility = 1.7
targetCompatibility = 1.7

dependencies {
    compile  "org.embulk:embulk-core:0.8.26"
    provided "org.embulk:embulk-core:0.8.26"
    compile  "org.embulk.input.jdbc:embulk-input-jdbc:0.8.2"
    compile  "org.apache.calcite:calcite-core:1.12.0"

    testCompile "junit:junit:4.+"
    testCompile "org.embulk:embulk-core:0.8.26:tests"
    testCompile "org.embulk:embulk-standards:0.8.26"
    testCompile "org.embulk:embulk-test:0.8.26"
}

task classpath(type: Copy, dependsOn: ["jar"]) {
    doFirst { file("classpath").deleteDir() }
    from (configurations.runtime - configurations.provided + files(jar.archivePath))
    into "classpath"
}
clean { delete "classpath" }

def checkstyleConfigDir = new File(rootDir, "/config/checkstyle")
checkstyle {
    configFile = new File(checkstyleConfigDir, "checkstyle.xml")
    configProperties.checkstyleConfigDir = checkstyleConfigDir
    toolVersion = '7.6.1'
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
check.dependsOn('checkstyle')

tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}
findbugs {
    ignoreFailures = true
}

task gem(type: JRubyExec, dependsOn: ["gemspec", "classpath"]) {
    jrubyArgs "-rrubygems/gem_runner", "-eGem::GemRunner.new.run(ARGV)", "build"
    script "${project.name}.gemspec"
    doLast { ant.move(file: "${project.name}-${project.version}.gem", todir: "pkg") }
}

task gemPush(type: JRubyExec, dependsOn: ["gem"]) {
    jrubyArgs "-rrubygems/gem_runner", "-eGem::GemRunner.new.run(ARGV)", "push"
    script "pkg/${project.name}-${project.version}.gem"
}

task "package"(dependsOn: ["gemspec", "classpath"]) << {
    println "> Build succeeded."
    println "> You can run embulk with '-L ${file(".").absolutePath}' argument."
}

task gemspec {
    ext.gemspecFile = file("${project.name}.gemspec")
    inputs.file "build.gradle"
    outputs.file gemspecFile
    doLast { gemspecFile.write($/
Gem::Specification.new do |spec|
  spec.name          = "${project.name}"
  spec.version       = "${project.version}"
  spec.authors       = ["Muga Nishizawa"]
  spec.summary       = %[Apache Calcite filter plugin for Embulk]
  spec.description   = %[Translate schema and column values by SQL-like query provided by Apache Calcite.]
  spec.email         = ["muga.nishizawa@gmail.com"]
  spec.licenses      = ["Apache 2.0"]
  spec.homepage      = "https://github.com/muga/embulk-filter-calcite"

  spec.files         = `git ls-files`.split("\n") + Dir["classpath/*.jar"]
  spec.test_files    = spec.files.grep(%r"^(test|spec)/")
  spec.require_paths = ["lib"]

  spec.add_development_dependency 'bundler', ['~> 1.0']
  spec.add_development_dependency 'rake', ['>= 10.0']
end
/$)
    }
}
clean { delete "${project.name}.gemspec" }
