plugins {
    id "java"
    id "maven-publish"
    id "org.embulk.embulk-plugins" version "0.4.2"
    id "checkstyle"
    id "jacoco"
}

repositories {
    mavenCentral()
    jcenter()
    maven { url "https://dl.bintray.com/embulk-input-jdbc/maven" }  // for embulk-input-jdbc
}

group = "org.embulk"
version = "0.4.0-SNAPSHOT"
description = "Translate schema and column values by SQL-like query provided by Apache Calcite."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:0.10.3"
    compileOnly "org.embulk:embulk-core:0.10.3"

    compile("org.embulk.input.jdbc:embulk-input-jdbc:0.11.0") {
        exclude group: "org.embulk", module: "embulk-core"
    }
    compile("org.apache.calcite:calcite-core:1.13.0") {
        // They conflict with embulk-core.
        exclude group: "org.slf4j", module:"slf4j-api"
        exclude group: "org.apache.commons", module: "commons-lang3"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-databind"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-annotations"
        exclude group: "com.fasterxml.jackson.core", module: "jackson-core"
        exclude group: "com.google.guava", module: "guava"
    }

    testCompile "junit:junit:4.13"
    testCompile "org.embulk:embulk-api:0.10.5"
    testCompile "org.embulk:embulk-core:0.10.5"
    testCompile "org.embulk:embulk-core:0.10.5:tests"
    testCompile "org.embulk:embulk-standards:0.10.5"
    testCompile "org.embulk:embulk-junit4:0.10.5"

    // TODO: Remove them.
    // They are now required because the dependency libraries of them are behind sub ClassLoaders.
    testCompile "org.embulk:embulk-deps-buffer:0.10.5"
    testCompile "org.embulk:embulk-deps-config:0.10.5"
    testCompile "org.embulk:embulk-deps-timestamp:0.10.5"
}

embulkPlugin {
    mainClass = "org.embulk.filter.calcite.CalciteFilterPlugin"
    category = "filter"
    type = "calcite"
}

jar {
    from rootProject.file("LICENSE")
}

sourcesJar {
    from rootProject.file("LICENSE")
}

javadocJar {
    from rootProject.file("LICENSE")
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            groupId = "${project.group}"
            artifactId = "${project.name}"

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = "${project.name}"
                description = "${project.description}"
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "The Apache License, Version 2.0"
                        url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                    }
                }

                developers {
                    developer {
                        name = "Muga Nishizawa"
                        email = "muga.nishizawa@gmail.com"
                    }
                    developer {
                        name = "Dai MIKURUBE"
                        email = "dmikurube@treasure-data.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/embulk/embulk-filter-calcite.git"
                    developerConnection = "scm:git:git@github.com:embulk/embulk-filter-calcite.git"
                    url = "https://github.com/embulk/embulk-filter-calcite"
                }
            }
        }
    }
    repositories {
        // TODO: Configure some public Maven repo(s).
    }
}

gem {
    authors = [ "Muga Nishizawa", "Dai MIKURUBE" ]
    email = [ "muga.nishizawa@gmail.com", "dmikurube@treasure-data.com" ]
    summary = "Apache Calcite filter plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-filter-calcite"
    licenses = [ "Apache-2.0" ]
}

gemPush {
    host = "https://rubygems.org"
}

def checkstyleConfigDir = new File(rootDir, "/config/checkstyle")
checkstyle {
    configFile = new File(checkstyleConfigDir, "checkstyle.xml")
    configProperties.checkstyleConfigDir = checkstyleConfigDir
    toolVersion = '7.6.1'
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
check.dependsOn('checkstyle')

jacocoTestReport {
    reports {
        html.enabled = true
    }
}
